global args = [];
global filesystem = table {
    "home" = table  {};
    "bin" = table  {
        "gcsh" = builtin_file("gcsh.gc");
        "bash" = builtin_file("bash.gc");
        "fish" = builtin_file("bash.gc");
        "zsh" = builtin_file("bash.gc");
        "sh" = builtin_file("bash.gc");
        "ls" = builtin_file("ls.gc");
        // TODO: neofetch, sl
    };
};

// ---------------------------------------- API ------------------------------------------ //
fn get_root_path(relative_path: String) {
    // TODO: ./../
    if relative_path[0] == "." {
        relative_path[0] = gcsh_working_directory;
    } else if relative_path[0] == "~" {
        relative_path[0] = gcsh_home_directory;
    }
    return relative_path;
}

fn read_file(path: String) {
    path = get_root_path(path);
    path[0] = "";

    let file = any;
    file = filesystem;
    if len(path) > 0  {
        if path[len(path) - 1] == "/" {
            path[len(path) - 1] = "";
        }

        let filename = "";
        for(path, fn (char: String) {
            if char == "/" {
                file = file[filename];
                filename = "";
            } else {
                filename = filename + char;
            }
        });
        file = file[filename];
    }
    return file;
}

// TODO: parse args like -l -la --list and stuff

// ------------------------------------ Interpreter -------------------------------------- //
global gcsh_command = "";
global gcsh_executable = "";
global gcsh_home_directory = "/home";
global gcsh_working_directory = gcsh_home_directory;

fn gcsh_execute() {
    gcsh_command = trim(gcsh_command);
    if len(gcsh_command) > 0 {
        if gcsh_command[0] != "#" {
            let args = [];
            {
                let token = "";
                for(gcsh_command, fn (char: String) {
                    // TODO: String literals
                    if char == " " {
                        args = args + [token];
                        token = "";
                    } else {
                        token = token + char;
                    }
                });
                args = args + [token];
            }

            gcsh_executable = "";
            let bin = filesystem["bin"];
            if contains(bin, args[0]) {
                eval(bin[args[0]]);
            } else {
                println("Command '" + args[0] + "' not found!");
            }
        }
    }
}

// --------------------------------------- Shell ----------------------------------------- //
if len(args) > 1 {
    args = remove(args, 0);
    let script = read_file(args[0]);
    gcsh_command = "";
    for(script, fn (char: String) {
        if char == "\n" {
            gcsh_execute();
            gcsh_command = "";
        } else {
            gcsh_command = gcsh_command + char;
        }
    });
    gcsh_execute();
} else {
    if gcsh_executable == "" {
        print("/home\n> ");
        gcsh_executable = "/bin/gcsh";
        gcsh_command = "";
    } else if gcsh_executable == "/bin/gcsh" {
        let input = input();
        if len(input) > 0 {
            if contains(input, "\x08") {
                if len(gcsh_command) > 0 {
                    screen_buffer = pop(screen_buffer);
                    gcsh_command = pop(gcsh_command);
                }
            } else if contains(input, "\n") {
                println();
                gcsh_execute();
            } else {
                gcsh_command = gcsh_command + input;
                print(input);
            }
        }
    } else {
        eval(read_file(gcsh_executable));
    }
}

